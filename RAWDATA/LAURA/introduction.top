topic: ~introduction keep (~greetings ~emohow_open ~emohow_closed ~speak_verbs ~narrate ~happening familie ~home tag)

t: $event_state=0 [Hallo!][Hey!][Grüß Gott!]

u: (~greetings) Ich bin Laura und möchte dir etwas erzählen.

#! Wie geht es dir?
#! Ist alles in Ordnung?
u: CLOSED_SITUATION(<<~emohow_closed ~bot>>) $closed_question_emo=1 ^respond(~answer_roulette)
  #! Warum geht es dir denn nicht so gut?
  a: (~why) $narrative=1 ^respond(~event_uncle)

#! Was beschäftigt dich denn gerade?
#! Was ist los bei dir?
u: OPENLY_SITUATION(<<~emohow_open ~bot>>) $narrative=1 ^respond(~topic_roulette)

#! Worüber möchtest du heute mit mir sprechen? 
#(PLACEHOLDER CHECKEN)
u: (<~why * ~narrate) ^reuse(OPENLY_SITUATION)

#! Erzähl mir von deinem Tag.
#! Erzähl mir doch mal, wie sieht ein typischer Tag bei dir aus?
u: (<<~narrate tag>>) ^reuse(OPENLY_SITUATION)

#! Möchtest du über deine Eltern sprechen?
u: TOPIC_CHOICE(über *1 _*1 ~narrate) ^refine()
  a:(_0?~parents) $topic = parents ^respond(~topic_roulette)
  a:(_0==onkel) $topic = uncle ^respond(~topic_roulette)
  a:(_0==bruder) $topic = brother ^respond(~topic_roulette)
  a:() Nein, darüber möchte ich nicht sprechen.

#! Ist dir etwas in der Schule/zu Hause/mit deinen Eltern/mit deinem Onkel passiert?
u: WHAT_HAPPEND_CLOSED (<<was {~places ~people} ~happening>>) $closed_question=1 ^respond(~answer_roulette)  

#! Was ist dir denn passiert?
u: WHAT_HAPPEND_OPEN (<was *~3  ~happening) $narrative=1 ^respond(~event_uncle)

#! Gibt es vielleicht Stress mit jemandem aus deiner Klasse.
u: PROBLEMS (<<~problems [mit in beim]>>) $closed_question_prob=1 ^respond(~answer_roulette)
  a: (<<oder [mit in]>>) Ich habe ja gesagt, dass ich keine Probleme habe.

#! Erzähl mir von zu Hause.
#! Erzähl ruhig mal, wie das dann so bei dir zuhause ist. 
#! Erzähl mir was ihr zusammen macht, wenn du zuhause bist. 
u: OPENLY_SITUATION_HOME(<<~narrate ~home>>) $openly_situation_home=1 ^respond(~topic_roulette)

#! Wie läuft es in der Schule? 
u: (<<~narrate ~school>>) [^respond(~school_class)][^respond(~school_soz)]

#! Erzähl mir etwas über deine Familie. 
u: (<<~narrate familie>>) ^reuse(OPENLY_SITUATION_HOME)

#! Verbringst du gerne Zeit mit deiner Familie?
u: (<<~like ~activity familie>>) [Ja][Nein]

#! Möchtest du mir was erzählen?
u: (<<was ~narrate) $narrative=1 ^respond(~topic_roulette)

#! Was unternimmst du zusammen mit deiner Familie, z.B. am Wochenende? 
u: (<<~activity familie>>) ^reuse(OPENLY_SITUATION_HOME)

