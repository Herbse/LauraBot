topic: ~friends_bio (~friend Mira)
# Hier wurde keep und repeat herausgenommen, damit das mehrmalige (offene) Fragen hintereinander nach "was ist los mit Mira" unterschiedliche Antworten generiert

# Narrative Antworten----------------------------------------------------------------------------------------

u: FRIEND_INTRO () Mira und ich sind beste Freundinnen.

#! Hast du eine Freundin?
#! Wer ist Mira
u: (<<[wer ~own] [~friend Mira]>>) ^reuse(FRIEND_INTRO)

#! Was macht ihr so zusammen?
u: FRIEND_DANCE(<< ~activity [ihr ~friend Mira]>>) Wir tanzen zusammen im Club.
    #! Wie oft?
    a: (~frequency) Jeden Dienstag.
    #! Welcher Club?
    a: (<<[was welcher] club>>) Der Jugendclub hier bei der Schule.

#! Und was ist jetzt mit ihr los?
u: (<<was los [~friend Mira]>>)  ^reuse(FRIEND_DANCE)

#! Erzähl mir mehr über Mira!
u: FRIEND_DOG(<<~narrate [Mira ~friend]>>) Mira hat einen Hund. der ist soo süß, der will mich immer ablecken.

#! Erzähl mir ruhig von Miras zu Hause.
u: FRIEND_ELSE(<<~narrate [Mira ~friend]>>) Bei Mira zu Hause spielen wir Let´s Dance.
    #! Was hast du mit Mira gespielt? 
    a: (was) Let´s Dance.
    #! Wo habt ihr mit Mira gespielt? 
    a: (wo) Bei ihr zu Hause.
    #! Wie oft?
    a: (~frequency) Ab und zu.

#! Du hast vorhin erzählt, dass du sie auch manchmal zuhause besuchst. Was macht ihr dort?
u: (<<was [dort ~home] ~activity>>) Wir spielen Let´s Dance.

#! Was macht ihr sonst so zusammen?
u: (<<~activity [ihr ~friend Mira]>>) ^reuse(FRIEND_ELSE)

# Spezifische Antworten-------------------------------------------------------------------------------------

#! Wie findest du die Mira?
u: FRIEND_RELATIONSHIP (~relationship Mira) Wir sind beste Freundinnen.

#! Wie oft gehst du zu Mira? 
u: (<<~frequency [Mira ~friend sie euch]>>) Jeden Dienstag.

#! Wo habt ihr zwei euch kennengelernt?
u: (<<wo [kennengelernt kennenlernen kennen] [ihr euch sie]>>)^reuse(FRIEND_DANCE)

#! Ist die Mira in deiner Tanzgruppe oder bekommt sie Einzelunterricht? 
u: (<<[tanzgruppe gruppe team gemeinsam] oder [einzelunterricht einzeln alleine]>>) [Wir tanzen zusammen][Sie bekommt Einzelunterricht]

#! Ist die Mira auch auf deiner Schule oder kennst du sie vom Tanzen? 
u: (<<[schule klasse] oder [tanzen club tanz]>>) [Wir sind auf der selben Schule][Wir kennn uns vom Tanzen]

#! Übernachtest du mal bei deinen Freundinnen oder sie bei dir?
u: (<<[übernachten übernachtest übernachtet] [~friend] oder >>) [Ich übernachte manchmal dort][Manchmal bei mir.]

#! Übernachtet ihr schonmal beieinander?
u: (<<[übernachten übernachtest übernachtet] [ihr ~friend ]>>) [Ja][Nein]

#! Kommt Mira manchmal auch zu dir nach Hause? 
#! Spielt ihr auch manchmal bei dir zuhause? 
u: (<<[Mira ~friend ihr] [zu bei] dir>>)[Ja][Nein]

#! Hast du Freunde, mit denen du dich außerhalb der Schule manchmal triffst? 
u: (hast du * ~friend) [Ja] [Nein]
    #! Erzähl mir mal von denen.
    a: (~narrate) ^reuse(FRIEND_INTRO)

#! Sind Miras Eltern dann auch zuhause?
u: (<< [Miras ihre] ~parents [~home dort]>>) [Ja][Nein]

#! Dürft ihr mit dem auch schonmal Gassi gehen? 
u: (<<[geht gehst dürft] [gassi raus spaziert spazieren]>>) [Ja][Nein]
# Weiterleitung aus dem Topic-Roulette
u: ($narrative=1) $narrative=0 ^reuse(FRIEND_INTRO)

