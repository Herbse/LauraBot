topic: ~friends_t ()
# Hier wurde keep und repeat herausgenommen, damit das mehrmalige (offene) Fragen hintereinander nach dem Tanzen  unterschiedliche Antworten generiert

#t: specific test to hobbies topic ^keep() ^repeat() 

#specific questions/answers depending on general context
u: ([freunde die sie]) ^keep() ^refine()
    # name
    a: (^names()) $$intent=names $$sound_file = s_luisa_friends1 Kathi und Michelle.
    # age
    a: (^age()) $$intent=age $$sound_file = s_luisa_friends2 Die sind so alt wie ich.
    # mood
    a: (^mood()) $$intent=mood $$sound_file = g_luisa_gut Gut.
    # job
    a: (^job()) $$intent=job 
    # relationship_f
    a: (^relationship_f()) $$intent=relationship_f $$sound_file = s_luisa_friends3 Das sind meine Freundinnen.
    # relationship_o
    a: (^relationship_o()) $$intent=relationship_o $$sound_file = s_luisa_friends4 Die sind cool.
    # time together
    a: (^time_together()) $$intent=time_together $$sound_file = s_luisa_friends5 In der Schule und in meiner Freizeit.
    # time relationship
    a: (^time()) $$intent=time_relationship $$sound_file = s_luisa_friends6 Seit ich in die Schule gehe.
    # activity together
    a: (^activity_together()) $$intent=activity_together ^reuse(FRIENDS_2)
    # activity
    a: (^activity()) $$intent=activity $$sound_file = s_luisa_friends7 Die gehen mit mir zur Schule.
    # places together
    a: (^places_together()) $$intent=places_together $$sound_file = s_luisa_friends8 Wir gehen in die gleiche Klasse und zusammen auf den Wasserspielplatz.


#specific questions/answers depending on what narrative answer was given before
u: () ^keep() ^refine()
    # personal_opinion
    a: (^personal_opinion()) $$intent=personal_opinion ^refine()
        b: ($friendCounter==1) $$sound_file = g_luisa_volldoof Voll doof. 
        b: ($friendCounter==2) $$sound_file = g_luisa_dasmachtspaß Das macht Spaß!
        b: ($friendCounter==3) $$sound_file = g_luisa_volldoof Voll doof. 
        b: ($friendCounter==4) $$sound_file = g_luisa_volldoof Voll doof. 
       
    # other_opinion
    a: (^other_opinion()) $$intent=other_opinion $$answerCategory=ignorance ^respond(~ignorance)
    # place
    a: (^place()) $$intent=place $$answerCategory=ignorance ^respond(~ignorance)
    # individual
    a: (^individual()) $$intent=individual ^refine()
        b: ($friendCounter==1) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($friendCounter==2) $$sound_file = s_luisa_friends9 Kathi, Michelle und ich.
        b: ($friendCounter==3) $$sound_file = s_luisa_friends9 Kathi, Michelle und ich.
        b: ($friendCounter==4) $$answerCategory=ignorance ^respond(~ignorance)
    # reason
    a: (^reason()) $$intent=reason $$answerCategory=ignorance ^respond(~ignorance)
    # example
    a: (^example()) $$intent=example ^refine()
        b: () $$nextAnswer = yes
    # explanation
    a: (^explanation()) $$intent=explanation $$nextAnswer = yes
    
    # follow-up
    a: (^follow_up()) $$intent=follow_up ^refine()
        b: () $$nextAnswer = yes
    # log questions that were not catched by macros
    a: ($$answerCategory==specific _*) $$question = '_0 ^log(FILE USERS/lostspecificlog.txt $$question \n)

# Narrative Antworten----------------------------------------------------------------------------------------

#! Erzähl mal von deinen Hobbies.
u: FRIENDS_1 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 1 $$sound_file = n_luisa_friends1
Kathi und Michelle kommen nie zu mir. Mama erlaubt das nicht.
 

  
#! Erzähl mal mehr.
u: FRIENDS_2 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 2 $$sound_file = n_luisa_friends2
Im Sommer gehen wir immer auf den Wasserspielplatz bei uns in der Nähe.
   
 

#! Erzähl mir mehr 
u: FRIENDS_3 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 3 $$sound_file = n_luisa_friends3
Wenn wir spielen, dann nur draußen oder bei Kathi zu Hause.
  

#!  Erzähl mir mehr.
u: FRIENDS_4 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 4 $$sound_file = n_luisa_friends4
Michelle übernachtet auch bei Kathi. Ich darf das aber nicht.
   

# Gebe eine ausweichende Antwort, wenn Topic erschöpft ist.
u: () ^emptyTopic()
