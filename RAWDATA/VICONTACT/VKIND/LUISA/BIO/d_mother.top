topic: ~mother_t repeat ()

#specific questions/answers depending on general context
u: () ^keep() ^refine()
    # name
    a: (^names()) $$intent=names Christina.
    # age
    a: (^age()) $$intent=age Die ist 50 Jahre alt.
    # mood
    a: (^mood()) $$intent=mood Gut.
    # job
    a: (^job()) $$intent=job Weiß nicht.
    # relationship_f
    a: (^relationship_f()) $$intent=relationship_f Die ist meine Mama.
    # relationship_o
    a: (^relationship_o()) $$intent=relationship_o Mama ist so streng.
    # time together
    a: (^time_together()) $$intent=time_together Immer wenn ich nach Hause komme.
    # time relationship
    a: (^time()) $$intent=time_relationship 
    # activity together
    a: (^activity_together()) $$intent=activity_together ^reuse(MAMA_3)
    # activity
    a: (^activity()) $$intent=activity Weiß nicht.
    # places together
    a: (^places_together()) $$intent=places_together Wir gehen zusammen in die Kirche.



    
#specific questions/answers depending on what narrative answer was given before
u: () ^keep() ^refine()
    # personal_opinion
    a: (^personal_opinion()) $$intent=personal_opinion ^refine()
        b: ($motherCounter==1) ^reuse(MAMA_2)
        b: ($motherCounter==2) Voll komisch.
        b: ($motherCounter==3) Langweilig.
        b: ($motherCounter==4) Voll doof.
       
    # other_opinion
    a: (^other_opinion()) $$intent=other_opinion $$answerCategory=ignorance ^respond(~ignorance)
    # place
    a: (^place()) $$intent=place ^refine()
        b: ($motherCounter==1) Zu Hause.
        b: ($motherCounter==2) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==3) Bei uns im Viertel.
        b: ($motherCounter==4) $$answerCategory=ignorance ^respond(~ignorance)
    # individual
    a: (^individual()) $$intent=individual $$intent=individual ^refine()
        b: ($motherCounter==1) Mama und ich.
        b: ($motherCounter==2) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==3) Mama, meine Brüder und ich.
        b: ($motherCounter==4) $$answerCategory=ignorance ^respond(~ignorance)
    # reason
    a: (^reason()) $$intent=reason ^refine()
        b: ($motherCounter==1) ^reuse(MAMA_2)
        b: ($motherCounter==2) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==3) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==4) $$answerCategory=ignorance ^respond(~ignorance)
    # example
    a: (^example()) $$intent=example ^refine()
        b: () $$nextAnswer = yes 
    # explanation
    a: (^explanation()) $$intent=explanation $$nextAnswer = yes
    
    # follow-up
    a: (^follow_up()) $$intent=follow_up ^refine()
        b: () $$nextAnswer = yes
    # log questions that were not catched by macros
    a: ($$answerCategory==specific _*) $$question = '_0 ^log(FILE USERS/lostspecificlog.txt $$question \n)

# Narrative Antworten----------------------------------------------------------------------------------------

u: MAMA_1 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 1 Mama ist sehr streng. Jeden Morgen und jeden Abend muss ich mit ihr beten.

#! Erzähl mir mehr über deine Mama!
u: MAMA_2 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 2 Ich mag das aber nicht. Das ist so, als würde ich mit mir selbst reden.
#! Und was machst du gerne mit deiner Mama?
u: MAMA_3 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 3 Jeden Sonntag gehen wir in die Kirche. Da ist es so langweilig.

#! Erzähl mal, wie das dann ist, wenn du dort deine Hausaufgaben machst. 
u: MAMA_4 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 4 Wenn ich Mama erzähle, dass ich später mal Künstlerin sein will, dann sagt sie, dass man damit kein Geld verdienen kann.

# Gebe eine ausweichende Antwort, wenn Topic erschöpft ist.
u: () ^emptyTopic()

