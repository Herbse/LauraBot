topic: ~friends_t ()
# Hier wurde keep und repeat herausgenommen, damit das mehrmalige (offene) Fragen hintereinander nach dem Tanzen  unterschiedliche Antworten generiert

#t: specific test to hobbies topic ^keep() ^repeat() 

#specific questions/answers depending on general context
u: () ^keep() ^refine()
    # name
    a: (^names()) $$intent=names $$sound_file = s_jason_friends1
    Kevin. 
    # age
    a: (^age()) $$intent=age $$sound_file = s_jason_friends2
    Der ist so alt wie ich.
    # mood
    a: (^mood()) $$intent=mood $$sound_file = n_jason_gut
    Gut.
    # job
    a: (^job()) $$intent=job $$sound_file = s_jason_friends3
    Der geht auch in die Schule.
    # relationship_f
    a: (^relationship_f()) $$intent=relationship_f $$sound_file = s_jason_friends4
    Der ist mein bester Freund. 
    # relationship_o
    a: (^relationship_o()) $$intent=relationship_o $$sound_file = s_jason_friends5
    Kevin ist voll cool!
    # time together
    a: (^time_together()) $$intent=time_together $$sound_file = s_jason_friends6
    Am Wochenende oder in meiner Freizeit. 
    # time relationship
    a: (^time()) $$intent=time_relationship $$sound_file = s_jason_friends7
    Seit ich zur Gruppe gehe. 
    # activity together
    a: (^activity_together()) $$intent=activity_together ^reuse(FRIENDS_3)
    # activity
    a: (^activity()) $$intent=activity $$sound_file = s_jason_friends3
    Der geht auch in die Schule.
    # places together
    a: (^places_together()) $$intent=places_together $$sound_file = s_jason_friends8
    In der Gruppentherapie oder draußen. 


#specific questions/answers depending on what narrative answer was given before
u: () ^keep() ^refine()
    # personal_opinion
    a: (^personal_opinion()) $$intent=personal_opinion ^refine()
        b: ($friendCounter==1) $$sound_file = g_jason_vollcool Voll cool.
        b: ($friendCounter==2) $$sound_file = g_jason_vollcool Voll cool.
        b: ($friendCounter==3) $$sound_file = g_jason_dasmachtspaß Das macht Spaß!
        b: ($friendCounter==4) $$sound_file = g_jason_dasmachtspaß Das macht Spaß!
       
    # other_opinion
    a: (^other_opinion()) $$intent=other_opinion ^respond(~ignorance)
    # place
    a: (^place()) $$intent=place ^refine()
        b: ($friendCounter==1) $$sound_file = g_jason_beidergruppe Bei der Gruppe.
        b: ($friendCounter==2) $$answerCategory=missing ^respond(~ignorance)
        b: ($friendCounter==3) $$sound_file = s_jason_aunt6 Zuhause oder draußen.
        b: ($friendCounter==4) $$sound_file = s_jason_aunt6 Zuhause oder draußen.
    # individual
    a: (^individual()) $$intent=individual ^refine()
        b: ($friendCounter==1) $$answerCategory=missing ^respond(~ignorance)
        b: ($friendCounter==2) $$sound_file = s_jason_friends9 Die anderen Kinder und ich.
        b: ($friendCounter==3) $$sound_file = s_jason_friends10 Kevin und ich.
        b: ($friendCounter==4) $$sound_file = s_jason_friends10 Kevin und ich.
    # reason
    a: (^reason()) $$intent=reason ^refine()
        b: ($friendCounter==1) $$answerCategory=missing ^respond(~ignorance)
        b: ($friendCounter==2) $$answerCategory=missing ^respond(~ignorance)
        b: ($friendCounter==3) ^reuse(FRIENDS_4)
        b: ($friendCounter==4) $$answerCategory=missing ^respond(~ignorance)
    # example
    a: (^example()) $$intent=example ^refine()
        b: () $$nextAnswer = yes
    # explanation
    a: (^explanation()) $$intent=explanation ^refine()
        b: ($friendCounter==2) ^respond(~therapy_t.THERAPY_1)
        b: ($friendCounter==4) $$sound_file = s_jason_friends11 Das ist ein Spiel mit Fantasietieren.
        b: () $$nextAnswer = yes
    
    # follow-up
    a: (^follow_up()) $$intent=follow_up ^refine()
        b: () $$nextAnswer = yes
    # log questions that were not catched by macros
    a: ($$answerCategory==specific _*) $$question = '_0 ^log(FILE USERS/lostspecificlog.txt $$question \n)

# Narrative Antworten----------------------------------------------------------------------------------------

#! Erzähl mal von deinen Hobbies.
u: FRIENDS_1 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 1 $$sound_file = n_jason_friends1
Kevin ist mein bester Freund geworden.
 

  
#! Erzähl mal mehr.
u: FRIENDS_2 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 2 $$sound_file = n_jason_friends2
Er geht auch in die Gruppe.
   
 

#! Erzähl mir mehr 
u: FRIENDS_3 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 3 $$sound_file = n_jason_friends3
Wir zocken immer Spiele. Kevin ist richtig gut im Zocken.
  

#!  Erzähl mir mehr.
u: FRIENDS_4 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 4 $$sound_file = n_jason_friends4
Wir sind Team Blau bei Pokemon. Kevin hat die besten Pokemon!
   

# Gebe eine ausweichende Antwort, wenn Topic erschöpft ist.
u: () ^emptyTopic()