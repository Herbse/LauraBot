topic: ~mother_t repeat ()

#specific questions/answers depending on general context
u: () ^keep() ^refine()
    # name
    a: (^names()) $$intent=names $$sound_file = s_natalia_mother1
    Oksana.
    # age
    a: (^age()) $$intent=age $$sound_file = s_natalia_mother2
    Meine Mama ist 36 Jahre alt.
    # mood
    a: (^mood()) $$intent=mood $$sound_file = g_natalia_gut
    Gut.
    # job
    a: (^job()) $$intent=job $$sound_file = s_natalia_mother3
    Die ist immer zu Hause.
    # relationship_f
    a: (^relationship_f()) $$intent=relationship_f $$sound_file = s_natalia_mother4
    Meine Mama.
    # relationship_o
    a: (^relationship_o()) $$intent=relationship_o $$sound_file = s_natalia_mother5
    Ich hab meine Mama lieb.
    # time together
    a: (^time_together()) $$intent=time_together ^reuse(MAMA_2)
    # time relationship
    a: (^time()) $$intent=time_relationship 
    # activity together
    a: (^activity_together()) $$intent=activity_together ^reuse(MAMA_3)
    # activity
    a: (^activity()) $$intent=activity $$sound_file = s_natalia_mother3
    Die ist immer zu Hause.
    # places together
    a: (^places_together()) $$intent=places_together $$sound_file = s_natalia_mother6
    Zuhause oder wir gehen zusammen in den Park.



    
#specific questions/answers depending on what narrative answer was given before
u: () ^keep() ^refine()
    # personal_opinion
    a: (^personal_opinion()) $$intent=personal_opinion ^refine()
        b: ($motherCounter==1) $$sound_file = g_natalia_vollcool Voll cool.
        b: ($motherCounter==2) $$sound_file = g_natalia_dasmachtspaß Das macht Spaß!
        b: ($motherCounter==3) $$sound_file = g_natalia_dasmachtspaß Das macht Spaß!
        b: ($motherCounter==4) $$sound_file = g_natalia_vollcool Voll cool.
       
    # other_opinion
    a: (^other_opinion()) $$intent=other_opinion $$answerCategory=ignorance ^respond(~ignorance)
    # place
    a: (^place()) $$intent=place ^refine()
        b: ($motherCounter==1) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==2) $$sound_file = s_natalia_mother7 Im Park
        b: ($motherCounter==3) $$sound_file = s_natalia_mother7 Im Park
        b: ($motherCounter==4) $$sound_file = s_natalia_mother8 Im Supermarkt.
    # individual
    a: (^individual()) $$intent=individual $$intent=individual ^refine()
        b: ($motherCounter==1) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==2) $$sound_file = s_natalia_dogs8 Mama und ich. 
        b: ($motherCounter==3) $$sound_file = s_natalia_dogs8 Mama und ich. 
        b: ($motherCounter==4) $$sound_file = s_natalia_mother9 Ich alleine.
    # reason
    a: (^reason()) $$intent=reason ^refine()
        b: ($motherCounter==1) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==2) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==3) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($motherCounter==4) $$answerCategory=ignorance ^respond(~ignorance)
    # example
    a: (^example()) $$intent=example ^refine()
        b: () $$nextAnswer = yes 
    # explanation
    a: (^explanation()) $$intent=explanation $$nextAnswer = yes
    
    # follow-up
    a: (^follow_up()) $$intent=follow_up ^refine()
        b: () $$nextAnswer = yes
    # log questions that were not catched by macros
    a: ($$answerCategory==specific _*) $$question = '_0 ^log(FILE USERS/lostspecificlog.txt $$question \n)

# Narrative Antworten----------------------------------------------------------------------------------------

u: MAMA_1 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 1 $$sound_file = n_natalia_mother1
Mama bekommt oft Besuch. Sie hat viele Freundinnen und Freunde.

#! Erzähl mir mehr über deine Mama!
u: MAMA_2 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 2 $$sound_file = n_natalia_mother2
Wir gehen manchmal zusammen in den Park und spielen mit den Hunden.
#! Und was machst du gerne mit deiner Mama?
u: MAMA_3 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 3 $$sound_file = n_natalia_mother3
Neulich haben wir im Park Blumen gesammelt und daraus einen Blumenkranz gebastelt. Das war schön.

#! Erzähl mal, wie das dann ist, wenn du dort deine Hausaufgaben machst. 
u: MAMA_4 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 4 $$sound_file = n_natalia_mother4
Ich gehe manchmal für Mama einkaufen, wenn sie kocht.

# Gebe eine ausweichende Antwort, wenn Topic erschöpft ist.
u: () ^emptyTopic()
