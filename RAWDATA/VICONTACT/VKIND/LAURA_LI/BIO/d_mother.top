topic: ~mother_t repeat ()

#specific questions/answers depending on general context
u: () ^keep() ^refine()
    # name
    a: (^names()) $$intent=names Charlotte.
    # age
    a: (^age()) $$intent=age Die ist 42.
    # mood
    a: (^mood()) $$intent=mood Nicht so gut.
    # job
    a: (^job()) $$intent=job Mama ist eigentlich Friseurin.
    # relationship_f
    a: (^relationship_f()) $$intent=relationship_f Die ist meine Mama.
    # relationship_o
    a: (^relationship_o()) $$intent=relationship_o Ich hab meine Mama so lieb.
    # time together
    a: (^time_together()) $$intent=time_together Nach der Schule.
    # time relationship
    a: (^time()) $$intent=time_relationship Komische Frage.
    # activity together
    a: (^activity_together()) $$intent=activity_together ^refine()
        b: ($motherCounter==3) ^reuse(MAMA_4)
        b: ($motherCounter<3) ^reuse(MAMA_3)
        b: () Sonst eigentlich nix.
    # activity
    a: (^activity()) $$intent=activity ^reuse(MAMA_1)
    # places together
    a: (^places_together()) $$intent=places_together In Mamas Zimmer.



#specific questions/answers depending on what narrative answer was given before
u: () ^keep() ^refine()
    # personal_opinion
    a: (^personal_opinion()) $$intent=personal_opinion ^refine()
        b: ($motherCounter==1) Voll traurig.
        b: ($motherCounter==2) Voll traurig.
        b: ($motherCounter==3) Ganz ok.
        b: ($motherCounter==4) Ganz ok.
       
    # other_opinion
    a: (^other_opinion()) $$intent=other_opinion $$answerCategory=missing ^respond(~ignorance)
    # place
    a: (^place()) $$intent=place ^refine()
        b: ($motherCounter==1) $$answerCategory=missing ^respond(~ignorance)
        b: ($motherCounter==2) Zu Hause in Mamas Zimmer.
        b: ($motherCounter==3) In Mamas Zimmer.
        b: ($motherCounter==4) In Mamas Zimmer.
    # individual
    a: (^individual()) $$intent=individual $$intent=individual ^refine()
        b: ($motherCounter==1) $$answerCategory=missing ^respond(~ignorance)
        b: ($motherCounter==2) Meine Mama
        b: ($motherCounter==3) $$answerCategory=missing ^respond(~ignorance)
        b: ($motherCounter==4) $$answerCategory=missing ^respond(~ignorance)
    # reason
    a: (^reason()) $$intent=reason ^refine()
        b: ($motherCounter==1) $$nextAnswer = yes
        b: ($motherCounter==2) $$answerCategory=missing ^respond(~ignorance)
        b: ($motherCounter==3) $$answerCategory=missing ^respond(~ignorance)
        b: ($motherCounter==4) $$answerCategory=missing ^respond(~ignorance)
    # example
    a: (^example()) $$intent=example ^refine()
        b: ($motherCounter==4) $$answerCategory=missing ^respond(~ignorance)
        b: () $$nextAnswer = yes 
    # explanation
    a: (^explanation()) $$intent=explanation ^refine()
        b: ($motherCounter==4) $$answerCategory=missing ^respond(~ignorance)
        b: () $$nextAnswer = yes
    
    # follow-up
    a: (^follow_up()) $$intent=follow_up ^refine()
        b: ($motherCounter==4) $$answerCategory=missing ^respond(~ignorance)
        b: () $$nextAnswer = yes
    # log questions that were not catched by macros
    a: ($$answerCategory==specific _*) $$question = '_0 ^log(FILE USERS/lostspecificlog.txt $$question \n)

# Narrative Antworten----------------------------------------------------------------------------------------

u: MAMA_1 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 1 Meine Mama ist immer zu Hause. Sie ist schlimm krank.

#! Erz채hl mir mehr 체ber deine Mama!
u: MAMA_2 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 2 Sie muss im Bett liegen, wegen ihrem R체cken.

#! Und was machst du gerne mit deiner Mama?
u: MAMA_3 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 3 Nach der Schule setze ich mich zu Mama und mache Hausaufgaben.

#! Erz채hl mal, wie das dann ist, wenn du dort deine Hausaufgaben machst. 
u: MAMA_4 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $motherCounter = 4 Mama hilft mir dann immer bei Mathe.


u: () ^emptyTopic()

