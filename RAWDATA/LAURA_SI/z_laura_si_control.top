rename: @chatoutput @9


outputmacro: laura_si() 
^addtopic(~introduction_t)
$cs_control_main = ~control
$cs_control_post = ~addposthoc # test post-processing 
$cs_language = german
$cs_externaltag = ~xPOSTAGSENTENCE 
$userprompt = ^"%user: >"
$botprompt = ^"LAURA: "
$singleResponse = 1 # issue only 1 output message    
$cs_token =  #DO_NUMBER_MERGE | #DO_DATE_MERGE  | #DO_PROPERNAME_MERGE
topic: ~xrejoinder system repeat ()

# simple rejoinder
u: () nofail(topic rejoinder()) 

table: defaultbot (^name)
^createfact(^name defaultbot defaultbot)
DATA:
laura_si

topic: ~control system ()
# on startup, do introduction
u: ( %input<%userfirstline) 
	gambit(~introduction_t)


# [ question: open]
u: (\[ * \])
    ^respond(~prob_answer)
	^end(SENTENCE)

    


# get a master copy of input then do our normal copies

u: () # main per-sentence processing
	$$currenttopic = %topic		# get the current topic at start of volley
    $_responseCount = %response
	if ($singleResponse) {$_responseCount = 0} # disable this to respond to each sentence in a volley



	if ( %response == $_responseCount) {nofail(topic ^rejoinder())}

	if (%length == 0 AND %response == $_responseCount )
	{
		nofail(TOPIC ^gambit($$currenttopic))  # gambit current topic since no input (usually start of conversation)
	}

	if (%response == $_responseCount) { nofail(TOPIC ^respond($$currenttopic)) } # current topic tries to respond to his input

	if (%response == $_responseCount) # see if some other topic has keywords matching his input (given we have no response yet)
	{
		@8 = ^keywordtopics()	# get topics referred in input
		loop()
		{
			$$topic = first(@8subject)
			nofail(TOPIC ^respond($$topic))
			if (%response != 0) # stop when we find something to say
			{
				^end(RULE)	# we are done, this terminates the loop (not the rule)
			}
		}
	}	

	if (%response == $_responseCount) 
	{
		nofail(TOPIC respond(~keywordless))
	}

	# if we have rejoinders for what we said OR we asked a question, stop here
	if (%outputrejoinder OR %lastquestion) 
	{
		end(TOPIC)
	}

	if (%response == $_responseCount AND ^marked($$currenttopic)) { nofail(TOPIC ^gambit($$currenttopic)) } # gambit current topic since keywords match current topic

	if (%response == $_responseCount) # gambit from ANY matching topic
	{
		@8 = ^keywordtopics()	# get topics referred in input
		loop()
		{
			$$topic = first(@8subject)
				nofail(TOPIC ^Gambit($$topic))	# gambit in best matching topic
				if (%response != 0) # stop when we find something
				{
					^end(RULE)
				}
		}
	}
	if (%response == $_responseCount){ nofail(TOPIC ^gambit($$currenttopic)) } # gambit from current topic even though no keywords matched
	
	if (%response == $_responseCount) 
	{ 
		@8 = ^GambitTopics()		# all topics with gambits (excluding system topics)
		loop()
		{
			$$topic = pick(@8subject)
				nofail(TOPIC ^Gambit($$topic))	# try a topic at random
				if (%response != $_responseCount)
				{
					^end(RULE)
				}
		}
	}
	if (%response == $_responseCount) 
	{
		 ^repeat() 
		 I don't know what to say. 
	}
	
topic: ~addposthoc system () # should be called for post processing

t: () ^emoHandler()

t: ()  
	$lastOutput = ^response(1)

t: () ^oob()
   ^postprintbefore(  \[ $$oob \]) $$oob = null 


