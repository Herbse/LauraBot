topic: ~friends_t ()
# Hier wurde keep und repeat herausgenommen, damit das mehrmalige (offene) Fragen hintereinander nach dem Tanzen  unterschiedliche Antworten generiert

#t: specific test to hobbies topic ^keep() ^repeat() 

#specific questions/answers depending on general context
u: () ^keep() ^refine()
    # name
    a: (^names()) $$intent=names Kevin.
    # age
    a: (^age()) $$intent=age Der ist so alt wie ich.
    # mood
    a: (^mood()) $$intent=mood Gut.
    # job
    a: (^job()) $$intent=job Der geht auch in die Schule.
    # relationship_f
    a: (^relationship_f()) $$intent=relationship_f Der ist mein bester Freund. 
    # relationship_o
    a: (^relationship_o()) $$intent=relationship_o Kevin ist voll cool!
    # time together
    a: (^time_together()) $$intent=time_together Am Wochenende oder in meiner Freizeit. 
    # time relationship
    a: (^time()) $$intent=time_relationship Seit ich zur Gruppe gehe. 
    # activity together
    a: (^activity_together()) $$intent=activity_together ^reuse(FRIENDS_3)
    # activity
    a: (^activity()) $$intent=activity Der geht auch in die Schule.
    # places together
    a: (^places_together()) $$intent=places_together In der Gruppentherapie oder draußen. 


#specific questions/answers depending on what narrative answer was given before
u: () ^keep() ^refine()
    # personal_opinion
    a: (^personal_opinion()) $$intent=personal_opinion ^refine()
        b: ($friendCounter==1) Voll cool.
        b: ($friendCounter==2) Voll cool.
        b: ($friendCounter==3) Das macht Spaß!
        b: ($friendCounter==4) Das macht Spaß!
       
    # other_opinion
    a: (^other_opinion()) $$intent=other_opinion ^respond(~ignorance)
    # place
    a: (^place()) $$intent=place ^refine()
        b: ($friendCounter==1) Bei der Gruppe.
        b: ($friendCounter==2) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($friendCounter==3) Zuhause oder draußen.
        b: ($friendCounter==4) Zuhause oder draußen.
    # individual
    a: (^individual()) $$intent=individual ^refine()
        b: ($friendCounter==1) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($friendCounter==2) Die anderen Kinder und ich.
        b: ($friendCounter==3) Kevin und ich.
        b: ($friendCounter==4) Kevin und ich.
    # reason
    a: (^reason()) $$intent=reason ^refine()
        b: ($friendCounter==1) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($friendCounter==2) $$answerCategory=ignorance ^respond(~ignorance)
        b: ($friendCounter==3) ^reuse(FRIENDS_4)
        b: ($friendCounter==4) $$answerCategory=ignorance ^respond(~ignorance)
    # example
    a: (^example()) $$intent=example ^refine()
        b: () $$nextAnswer = yes
    # explanation
    a: (^explanation()) $$intent=explanation ^refine()
        b: ($friendCounter==2) ^respond(~therapy_t.THERAPY_1)
        b: ($friendCounter==4) Das ist ein Spiel mit Fantasietieren.
        b: () $$nextAnswer = yes
    
    # follow-up
    a: (^follow_up()) $$intent=follow_up ^refine()
        b: () $$nextAnswer = yes
    # log questions that were not catched by macros
    a: ($$answerCategory==specific _*) $$question = '_0 ^log(FILE USERS/lostspecificlog.txt $$question \n)

# Narrative Antworten----------------------------------------------------------------------------------------

#! Erzähl mal von deinen Hobbies.
u: FRIENDS_1 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 1 Kevin ist mein bester Freund geworden.
 

  
#! Erzähl mal mehr.
u: FRIENDS_2 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 2 Er geht auch in die Gruppe.
   
 

#! Erzähl mir mehr 
u: FRIENDS_3 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 3 Wir zocken immer Spiele. Kevin ist richtig gut im Zocken.
  

#!  Erzähl mir mehr.
u: FRIENDS_4 ([$$answerCategory==narrative $$answerCategory==specific $$answerCategory==facilitator]) $friendCounter = 4 Wir sind Team Blau bei Pokémon. Kevin hat die besten Pokémon!
   

# Spezifische Antworten------------------------------------------------------------------------------


